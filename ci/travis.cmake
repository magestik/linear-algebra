set(CTEST_SOURCE_DIRECTORY "$ENV{TRAVIS_BUILD_DIR}")
set(CTEST_BINARY_DIRECTORY "${CTEST_SOURCE_DIRECTORY}/build")

set(CTEST_SITE "travis-ci.com")
set(CTEST_BUILD_NAME "$ENV{TRAVIS_OS_NAME}-$ENV{TRAVIS_COMPILER}-default")

set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_BUILD_CONFIGURATION "Debug")

set(WITH_MEMCHECK TRUE)

set(TRAVIS_COMPILER "$ENV{TRAVIS_COMPILER}")
if (TRAVIS_COMPILER STREQUAL "gcc")
	set(CTEST_BUILD_OPTIONS "-DWITH_COVERAGE:BOOL=ON")
	set(WITH_COVERAGE TRUE)
else()
	set(CTEST_BUILD_OPTIONS "")
	set(WITH_COVERAGE FALSE)
endif()

find_program(CTEST_GIT_COMMAND NAMES git)
find_program(CTEST_COVERAGE_COMMAND NAMES gcov)
find_program(CTEST_MEMORYCHECK_COMMAND NAMES valgrind)

set(CTEST_UPDATE_COMMAND "${CTEST_GIT_COMMAND}")
set(CTEST_CONFIGURE_COMMAND "${CMAKE_COMMAND} -DCMAKE_BUILD_TYPE:STRING=${CTEST_BUILD_CONFIGURATION}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DCTEST_CMAKE_GENERATOR:STRING=${CTEST_CMAKE_GENERATOR}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} ${CTEST_BUILD_OPTIONS} \"${CTEST_SOURCE_DIRECTORY}\"")

ctest_start(Continuous)

ctest_update()

ctest_configure()

ctest_build()

ctest_test()

if (WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  ctest_coverage()
endif (WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)

if (WITH_MEMCHECK AND CTEST_MEMORYCHECK_COMMAND)
  ctest_memcheck()
endif (WITH_MEMCHECK AND CTEST_MEMORYCHECK_COMMAND)

ctest_submit()
